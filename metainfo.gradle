import java.nio.file.Files

task generateMetaInfo(type: TransformTask) {
  group 'codegen'
  description "generate meta information class"

  generateCode.dependsOn it

  def outputDir = file("gen/meta-info")
  def className = "MetaInfo"

  template = rootProject.file('metainfo.template.kt')
  output = file("$outputDir/$pckgDir/${className}.kt")
  model.className = className

  inputs.file rootProject.file("gradle.properties")
  inputs.file template
  outputs.dir outputDir

  project.sourceSets.main.java.srcDirs += outputDir
  project.sourceSets.main.kotlin.srcDirs += outputDir
  idea.module.generatedSourceDirs += outputDir

  doFirst {
    delete(outputDir)
    outputDir.mkdirs()
    // get current commit SHA
    try {
      new ByteArrayOutputStream().withStream { os ->
        exec {
          standardOutput = os
          commandLine = ['git', 'rev-parse', '--verify', 'HEAD']
          println "getting current commit SHA..."
          println "> ${commandLine.join(' ')}"
        }
        model.sha = new Scanner(os.toString()).nextLine()
        println "model.sha: ${model.sha}"
      }
    } catch (e) {
      println "failed to get current commit SHA: $e"
    }

    try {
      new ByteArrayOutputStream().withStream { os ->
        exec {
          standardOutput = os
          commandLine = ['git', 'tag', '--points-at', 'HEAD']
          println "getting current tags..."
          println "> ${commandLine.join(' ')}"
        }
        model.tags = os.toString().split('\n').findAll {!it.isAllWhitespace()}
        println "model.tags: ${model.tags}"
      }
    } catch (e) {
      println "failed to get current commit SHA: $e"
    }
  }

  clean.doFirst {
    delete(outputDir)
  }
}